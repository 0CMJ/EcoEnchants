plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
    id 'java'
}

dependencies {
    implementation getSubprojects()
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        maven { url 'https://jitpack.io' }
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://repo.codemc.org/repository/nms/' }
    }

    jar {
        onlyIf { !sourceSets.main.allSource.files.isEmpty() }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:19.0.0'
    }

    tasks.withType(JavaCompile) {
        options.deprecation = true
        options.encoding = 'UTF-8'
    }

    processResources {
        filesNotMatching(["**/*.png", "**/models/**", "**/textures/**"]) {
            expand projectVersion: project.version
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileJava.dependsOn clean
}

tasks.withType(Jar) {
    destinationDirectory = file("$rootDir/bin/")
}

clean.doLast {
    file("${rootDir}/bin").deleteDir()
}

shadowJar {
    relocate('org.bstats.bukkit', 'com.willfp.eco.util.shaded.bstats')
    relocate('org.apache.maven', 'com.willfp.eco.util.shaded.maven')
    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + ".jar"
}

jar {
    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + " " + "unshaded" + ".jar"
}

group = 'com.willfp'
archivesBaseName = project.name
version = findProperty("version")
java.sourceCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = 'UTF-8'

build.dependsOn shadowJar

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}